{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "05b027cb-2d88-4f0b-88ee-424c0ba440d0",
   "metadata": {},
   "outputs": [],
   "source": [
    "using Plots, ProgressMeter, CUDA"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "54332e30-dba7-4152-af84-c45bd7a23f61",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "ν = 0.012800001  τ = 0.5384\n"
     ]
    }
   ],
   "source": [
    "# Simulation parameters\n",
    "nx=129\n",
    "ny=nx\n",
    "ncells=nx*ny\n",
    "\n",
    "iterations=350000\n",
    "u0 = 0.1f0\n",
    "ρ0 = 1.0f0\n",
    "Re = 1000.0f0\n",
    "ν = (nx-1)*u0/Re # Kinematic viscosity (0.005 <= ν <= 0.2)\n",
    "τ = (6ν+1)/2\n",
    "\n",
    "println(\"ν = \", ν, \"  τ = \", τ)\n",
    "\n",
    "# Directions\n",
    "C=1; E=2; S=3; W=4; N=5; NE=6; SE=7; SW=8; NW=9\n",
    "dx = Int8[0, 1, 0, -1, 0, 1, 1, -1, -1]\n",
    "dy = Int8[0, 0, -1, 0, 1, 1, -1, -1, 1]\n",
    "\n",
    "opposite = Int8[1, 4, 5, 2, 3, 8, 9, 6, 7]\n",
    "\n",
    "# Geometry\n",
    "geometry=zeros(Int, ny,nx) # Fluid/wall cells\n",
    "geometry[1,:] .= 1         # top lid\n",
    "\n",
    "geom_lin = vec(geometry)\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "d6db551f-1460-400d-8bac-cfecbc9df0ad",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Host side distribution functions\n",
    "h_f = zeros(Float32, 9, ncells)\n",
    "h_feq = similar(h_f)\n",
    "h_fstar = similar(h_f)\n",
    "\n",
    "# Initial distribution set at equilibrium\n",
    "h_f[C, :] .= ρ0 * (4f0/9f0)\n",
    "for d in (E, S, W, N)\n",
    "    h_f[d, :] .= ρ0 * (1f0/9f0)\n",
    "end\n",
    "for d in (NE, SE, SW, NW)\n",
    "    h_f[d, :] .= ρ0 * (1f0/36f0)\n",
    "end\n",
    "\n",
    "# Move data over to GPU\n",
    "f = CuArray(h_f)\n",
    "feq = CuArray(h_feq)\n",
    "fstar = CuArray(h_fstar)\n",
    "geom_d = CuArray(Int32.(geom_lin))\n",
    "\n",
    "nx_d = Int32(nx)\n",
    "ny_d = Int32(ny)\n",
    "ncells_d = Int32(ncells)\n",
    "u0_d = Float32(u0)\n",
    "τ_d = Float32(τ)\n",
    "\n",
    "# Precompute dx, dy, opposite arrays on GPU\n",
    "dx_d = CuArray(Int8.(dx))\n",
    "dy_d = CuArray(Int8.(dy))\n",
    "opp_d = CuArray(Int8.(opposite))\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "8062cf2d-e4c8-40bb-85fc-e2f80e3be885",
   "metadata": {},
   "outputs": [],
   "source": [
    "# GPU kernels\n",
    "function collision_kernel!(f::CuDeviceMatrix{Float32}, fstar::CuDeviceMatrix{Float32}, τ::Float32, ncells::Int32)\n",
    "    tid = (blockIdx().x - 1) * blockDim().x + threadIdx().x\n",
    "    if tid < 1 || tid > ncells\n",
    "        return\n",
    "    end\n",
    "    \n",
    "    idx = tid\n",
    "    \n",
    "    fC  = f[1, idx]; fE  = f[2, idx]; fS  = f[3, idx]; fW  = f[4, idx]; fN  = f[5, idx]\n",
    "    fNE = f[6, idx]; fSE = f[7, idx]; fSW = f[8, idx]; fNW = f[9, idx]\n",
    "\n",
    "    ρ = fC + fE + fS + fW + fN + fNE + fSE + fSW + fNW\n",
    "    invρ = 1f0 / (ρ > 1f-12 ? ρ : 1f-12)\n",
    "    ux = (fE - fW + fNE + fSE - fSW - fNW) * invρ\n",
    "    uy = (fN - fS + fNE + fNW - fSE - fSW) * invρ\n",
    "\n",
    "    u2 = ux*ux + uy*uy\n",
    "\n",
    "    feqC = (4f0/9f0) * ρ * (1f0 - 1.5f0*u2)\n",
    "    feqE = (1f0/9f0) * ρ * (1f0 + 3f0*ux + 4.5f0*ux*ux - 1.5f0*u2)\n",
    "    feqS = (1f0/9f0) * ρ * (1f0 - 3f0*uy + 4.5f0*uy*uy - 1.5f0*u2)\n",
    "    feqW = (1f0/9f0) * ρ * (1f0 - 3f0*ux + 4.5f0*ux*ux - 1.5f0*u2)\n",
    "    feqN = (1f0/9f0) * ρ * (1f0 + 3f0*uy + 4.5f0*uy*uy - 1.5f0*u2)\n",
    "    up = ux + uy\n",
    "    um = ux - uy\n",
    "    feqNE = (1f0/36f0) * ρ * (1f0 + 3f0*(up) + 4.5f0*(up*up) - 1.5f0*u2)\n",
    "    feqSE = (1f0/36f0) * ρ * (1f0 + 3f0*(um) + 4.5f0*(um*um) - 1.5f0*u2)\n",
    "    feqSW = (1f0/36f0) * ρ * (1f0 + 3f0*(-up) + 4.5f0*(up*up) - 1.5f0*u2)\n",
    "    feqNW = (1f0/36f0) * ρ * (1f0 + 3f0*(-um) + 4.5f0*(um*um) - 1.5f0*u2)\n",
    "\n",
    "    ω = 1f0 / τ\n",
    "    invfactor = 1f0 - ω\n",
    "\n",
    "    fstar[1, idx] = fC*invfactor + feqC*ω\n",
    "    fstar[2, idx] = fE*invfactor + feqE*ω\n",
    "    fstar[3, idx] = fS*invfactor + feqS*ω\n",
    "    fstar[4, idx] = fW*invfactor + feqW*ω\n",
    "    fstar[5, idx] = fN*invfactor + feqN*ω\n",
    "    fstar[6, idx] = fNE*invfactor + feqNE*ω\n",
    "    fstar[7, idx] = fSE*invfactor + feqSE*ω\n",
    "    fstar[8, idx] = fSW*invfactor + feqSW*ω\n",
    "    fstar[9, idx] = fNW*invfactor + feqNW*ω\n",
    "    \n",
    "    return\n",
    "end\n",
    "\n",
    "function streaming_kernel!(f::CuDeviceMatrix{Float32}, fstar::CuDeviceMatrix{Float32}, geom::CuDeviceVector{Int32}, nx::Int32, ny::Int32, \n",
    "        ncells::Int32, dx_d::CuDeviceVector{Int8}, dy_d::CuDeviceVector{Int8}, opp_d::CuDeviceVector{Int8}, u0::Float32, ρ0::Float32)\n",
    "    tid = (blockIdx().x - 1) * blockDim().x + threadIdx().x\n",
    "    if tid < 1 || tid > ncells\n",
    "        return\n",
    "    end\n",
    "\n",
    "    idx = tid\n",
    "    j = (idx - 1) ÷ ny + 1\n",
    "    i = (idx - 1) % ny + 1\n",
    "\n",
    "    g = geom[idx]\n",
    "\n",
    "    if g == 1\n",
    "        # Lid cell, set f to equilibrium\n",
    "        ρ = f[1, idx] + f[2, idx] + f[3, idx] + f[4, idx] + f[5, idx] + f[6, idx]+ f[7, idx]+ f[8, idx] + f[9, idx]\n",
    "        ux = u0\n",
    "        uy = 0f0\n",
    "        u2 = ux*ux + uy*uy\n",
    "        \n",
    "        f[1, idx] = (4f0/9f0) * ρ * (1f0 - 1.5f0*u2)\n",
    "        f[2, idx] = (1f0/9f0) * ρ * (1f0 + 3f0*ux + 4.5f0*ux*ux - 1.5f0*u2)\n",
    "        f[3, idx] = (1f0/9f0) * ρ * (1f0 - 3f0*uy + 4.5f0*uy*uy - 1.5f0*u2)\n",
    "        f[4, idx] = (1f0/9f0) * ρ * (1f0 - 3f0*ux + 4.5f0*ux*ux - 1.5f0*u2)\n",
    "        f[5, idx] = (1f0/9f0) * ρ * (1f0 + 3f0*uy + 4.5f0*uy*uy - 1.5f0*u2)\n",
    "        up = ux + uy\n",
    "        um = ux - uy\n",
    "        f[6, idx] = (1f0/36f0) * ρ * (1f0 + 3f0*(up) + 4.5f0*(up*up) - 1.5f0*u2)\n",
    "        f[7, idx] = (1f0/36f0) * ρ * (1f0 + 3f0*(um) + 4.5f0*(um*um) - 1.5f0*u2)\n",
    "        f[8, idx] = (1f0/36f0) * ρ * (1f0 + 3f0*(-up) + 4.5f0*(up*up) - 1.5f0*u2)\n",
    "        f[9, idx] = (1f0/36f0) * ρ * (1f0 + 3f0*(-um) + 4.5f0*(um*um) - 1.5f0*u2)\n",
    "        return\n",
    "    end\n",
    "\n",
    "    @inbounds begin\n",
    "        for d in 1:9\n",
    "            ddx = Int32(dx_d[d])\n",
    "            ddy = Int32(dy_d[d])\n",
    "            ni = i - ddy\n",
    "            nj = j - ddx\n",
    "            if ni >= 1 && ni <= ny && nj >= 1 && nj <= nx\n",
    "                nidx = (nj - 1) * ny + ni\n",
    "                f[d, idx] = fstar[d, nidx]\n",
    "            else\n",
    "                opp = Int(opp_d[d])\n",
    "                f[d, idx] = fstar[opp, idx]\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "\n",
    "    return\n",
    "end\n",
    ";"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "64791a37-3e4c-4371-a752-503287882d86",
   "metadata": {},
   "outputs": [
    {
     "name": "stderr",
     "output_type": "stream",
     "text": [
      "\u001b[32mProgress:  34%|█████████████▉                           |  ETA: 0:00:08\u001b[39m:18\u001b[39m"
     ]
    }
   ],
   "source": [
    "# kernel compilation helper\n",
    "collision_gpu = @cuda threads=256 collision_kernel!(f, fstar, τ_d, Int32(ncells))\n",
    "\n",
    "# main time loop\n",
    "threads = 256\n",
    "blocks = cld(ncells, threads)\n",
    "\n",
    "@showprogress for it in 1:iterations\n",
    "    @cuda threads=threads blocks=blocks collision_kernel!(f, fstar, τ_d, Int32(ncells))\n",
    "    @cuda threads=threads blocks=blocks streaming_kernel!(f, fstar, geom_d, nx_d, ny_d, ncells_d, dx_d, dy_d, opp_d, u0_d, ρ0)\n",
    "\n",
    "    if (it % 1000) == 0\n",
    "        CUDA.synchronize()\n",
    "    end\n",
    "end\n",
    "CUDA.synchronize()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a23c2296-6365-4fd9-8c1a-377f77ae70d6",
   "metadata": {},
   "outputs": [],
   "source": [
    "f_final = Array(f)\n",
    "ρ_final = sum(f_final, dims=1)[1,:]\n",
    "\n",
    "ux = (f_final[2,:] - f_final[4,:] + f_final[6,:] + f_final[7,:] - f_final[8,:] - f_final[9,:]) ./ ρ_final\n",
    "uy = (f_final[5,:] - f_final[3,:] + f_final[6,:] + f_final[9,:] - f_final[7,:] - f_final[8,:]) ./ ρ_final\n",
    "\n",
    "ux = reshape(ux, ny, nx)\n",
    "uy = reshape(uy, ny, nx)\n",
    "u2 = ux.*ux + uy.*uy\n",
    "\n",
    "h = heatmap(reverse(sqrt.(u2)./u0, dims=1), c=:jet, aspect_ratio=:equal)\n",
    "plot(h, size=(600,600))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "f1f19d6e-13f9-4820-a62a-7cf085d65a1d",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "9×9 Matrix{Float64}:\n",
       " 1.0   0.0   0.0  -0.5   0.0  -0.5   0.0   0.0   0.5\n",
       " 1.0   3.0   0.0   1.0   0.0  -0.5  -1.5   0.0  -1.0\n",
       " 1.0   0.0   3.0  -0.5   0.0   1.0   0.0  -1.5  -1.0\n",
       " 1.0  -3.0   0.0   1.0   0.0  -0.5   1.5   0.0  -1.0\n",
       " 1.0   0.0  -3.0  -0.5   0.0   1.0   0.0   1.5  -1.0\n",
       " 1.0   3.0   3.0   1.0   3.0   1.0   3.0   3.0   2.0\n",
       " 1.0  -3.0   3.0   1.0  -3.0   1.0  -3.0   3.0   2.0\n",
       " 1.0  -3.0  -3.0   1.0   3.0   1.0  -3.0  -3.0   2.0\n",
       " 1.0   3.0  -3.0   1.0  -3.0   1.0   3.0  -3.0   2.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "c = [0 0;\n",
    "    1 0;\n",
    "    0 1;\n",
    "    -1 0;\n",
    "    0 -1;\n",
    "    1 1;\n",
    "    -1 1;\n",
    "    -1 -1;\n",
    "    1 -1]\n",
    "\n",
    "Ek(i) = reshape([1, 3c[i,1], 3c[i,2], \n",
    "    (3c[i,1]^2 - 1)/2, 3c[i,1]*c[i,2], (3c[i,2]^2 - 1)/2, \n",
    "    3c[i,1]*(3c[i,2]^2 - 1)/2, 3c[i,2]*(3c[i,1]^2 - 1)/2, (3c[i,1]^2 - 1)*(3c[i,2]^2 - 1)/2], (1,9))\n",
    "E = [\n",
    "    Ek(1); \n",
    "    Ek(2);\n",
    "    Ek(3);\n",
    "    Ek(4);\n",
    "    Ek(5);\n",
    "    Ek(6);\n",
    "    Ek(7);\n",
    "    Ek(8);\n",
    "    Ek(9)\n",
    "]\n",
    "display(E)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.7"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
